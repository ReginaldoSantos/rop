apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'eclipse'
// apply from:'https://raw.github.com/breskeby/gradleplugins/master/emmaPlugin/emma.gradle'
apply from:'emma.gradle'

group = 'com.github.ryenus'
version = '1.0'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories { mavenCentral() }

dependencies {
	emma "emma:emma:2.0.5312"
	emma "emma:emma_ant:2.0.5312"

	testCompile 'junit:junit:4.8.2'
}

test { systemProperties 'file.encoding': 'UTF-8' }

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives jar
	archives javadocJar
	archives sourcesJar
}

signing { sign configurations.archives }

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: sonatypeUsername, password: sonatypePassword)
			}

			pom.project {
				name 'Rop'
				description 'A lightweight command line option parser written in Java'
				url 'http://ryenus.github.com/rop'

				scm {
					url 'https://github.com/ryenus/rop'
					connection 'scm:git:https://github.com/ryenus/rop'
					developerConnection 'scm:git:git@github.com:ryenus/rop'
				}

				licenses {
					license {
						name 'MIT License'
						url 'http://www.opensource.org/licenses/MIT'
						distribution 'repo'
					}
				}

				developers {
					developer {
						id 'ryenus'
						name 'Roger Ye'
					}
				}

				properties {
					maven {
						compiler {
							source '1.7'
							target '1.7'
						}
					}
					project {
						build { sourceEncoding 'UTF-8' }
					}
				}
			}
		}
	}
}
